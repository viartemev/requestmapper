buildscript {

    ext {
        kotlin_version = '1.3.0'
        junit_jupiter_version = '5.3.1'
        junit_plugin_version = '1.2.0'
        spek_version = '1.2.1'
        assertj_version = '3.9.1'
        kluent_version = '1.36'
        log4j_version = '2.11.1'
        commons_lang3_version = '3.7'

        kotlinter_gradle_plugin = '1.11.1'
        gradle_intellij_plugin = '0.3.12'
    }

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath files('libs/ktlint-print-rule.jar')
        classpath "gradle.plugin.org.jmailen.gradle:kotlinter-gradle:$kotlinter_gradle_plugin"
        classpath "gradle.plugin.org.jetbrains.intellij.plugins:gradle-intellij-plugin:$gradle_intellij_plugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_plugin_version"
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

apply plugin: "org.jetbrains.intellij"
apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "idea"
apply plugin: "jacoco"
apply plugin: "org.junit.platform.gradle.plugin"
apply plugin: "org.jmailen.kotlinter"

group "com.viartemev.requestmapper"
version "0.14"

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version")
    compile("org.jetbrains.kotlin:kotlin-reflect:$kotlin_version")
    compile("org.apache.commons:commons-lang3:$commons_lang3_version")

    testCompile("org.jetbrains.spek:spek-api:$spek_version")
    testRuntime("org.jetbrains.spek:spek-junit-platform-engine:$spek_version")

    testCompile("org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version")
    testCompile("com.nhaarman:mockito-kotlin:1.5.0")

    // To use Log4J's LogManager
    testRuntime("org.apache.logging.log4j:log4j-core:$log4j_version")
    testRuntime("org.apache.logging.log4j:log4j-jul:$log4j_version")

    testImplementation "org.amshove.kluent:kluent:$kluent_version"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
    logManager 'org.apache.logging.log4j.jul.LogManager'
}

intellij {
    pluginName "Request mapper"
    type 'IC'
    version '2017.3'
}

jacoco {
    toolVersion = '0.7.9'
    applyTo junitPlatformTest
}

junitPlatformTest {
    jacoco {
        destinationFile = file("${buildDir}/jacoco/test.exec")
    }
}

patchPluginXml {
    version '0.14'
    sinceBuild '141.0'
    untilBuild ''
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}
